// Apply the gradle plugins
plugins {
    id 'org.springframework.boot' version '2.0.5.RELEASE'
}

// In this section you declare the dependencies for your production and test code
dependencies {
    // Process Application
    compile project(':' + rootProject.name + '-process-application')
    compile project(':' + rootProject.name + '-package-boot')

	// REST API
	compile group:'org.camunda.bpm.springboot', name: 'camunda-bpm-spring-boot-starter-rest'

    // WebApps
    compile group:'org.camunda.bpm.springboot', name: 'camunda-bpm-spring-boot-starter-webapp'

    // Db-Driver
    compile group: 'com.h2database', name: 'h2'
}

// Spring Boot
springBoot {
    mainClassName = 'org.camunda.bpm.CamundaProcessApplication'
}

// boot run configuration for dynamic profiles
bootRun.ext.activeProfiles = 'common,db-local'
bootRun {
    doFirst {
        jvmArgs = ["-Dspring.profiles.active=" + ext.activeProfiles]
    }
}

// Run against a local in-memory database
task bootRunAllInOneWithLocalDb {
    group = 'camunda'
    description "Alias to \":${bootRun.name}\" task: ${bootRun.description}"
    doFirst {
        bootRun.ext.activeProfiles = 'common,db-local'
    }
}
bootRunAllInOneWithLocalDb.finalizedBy bootRun

// Run against remote database
task bootRunAllInOneWithRemoteDb {
    group = 'camunda'
    description "Alias to \":${bootRun.name}\" task: ${bootRun.description}"
    doFirst {
        bootRun.ext.activeProfiles = 'common,db-remote'
    }
}
bootRunAllInOneWithRemoteDb.finalizedBy bootRun
