// Apply the gradle plugins
plugins {
    id 'war'
    id 'com.bmuschko.cargo' version '2.4'
}

dependencies {
    // Servlet API
    providedCompile group: 'javax.servlet', name: 'javax.servlet-api'

    // Process Application
    compile project(':' + rootProject.name + '-process-application')

    // Spring
    compile group: 'org.springframework', name: 'spring-webmvc'

    // Logging
    providedCompile group: 'org.slf4j', name: 'slf4j-jdk14'

    // Camunda BPM - Engine
    providedCompile group: 'org.camunda.bpm', name: 'camunda-engine'
}

// Tomcat Deployment Configuration
ext {
    camunda_serverType = 'tomcat8x'
    camunda_hostname = "tomcat.localhost"
    camunda_username = "tomcat"
    camunda_password = "secret"
}

// Build WAR with custom name
war {
    archiveName = 'process.war'
}

// Cargo Configuration
cargo {
    containerId = "$camunda_serverType"
    port = 80

    deployable {
        file = file('build/libs/process.war')
        context = rootProject.name
    }

    remote {
        hostname = "$camunda_hostname"
        username = "$camunda_username"
        password = "$camunda_password"
    }
}

// Deployment Tasks
task deployWarToSharedEngine {
    group 'camunda'
    description 'Builds the Process and runs the deployment to the shared engine.'

    dependsOn clean
    dependsOn war
    dependsOn cargoRedeployRemote
}

// avoid errors caused by multiple logger-bindings, also the shared engine already has slf4j which would cause a conflict
configurations {
    runtime.exclude group: "org.slf4j"
    compile.exclude group: "org.slf4j"
}
