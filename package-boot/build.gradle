// Apply the gradle plugins
plugins {
    id 'org.springframework.boot' version '2.0.5.RELEASE'
}

// In this section you declare the dependencies for your production and test code
dependencies {
    // Process Application
    compile project(':' + rootProject.name + '-process-application')

	// Camunda
	compile group: 'org.camunda.bpm.springboot', name: 'camunda-bpm-spring-boot-starter'
	testCompile group: 'org.camunda.bpm.springboot', name: 'camunda-bpm-spring-boot-starter-test'

    // Db-Driver
    compile group: 'com.h2database', name: 'h2'
}

// Spring Boot
springBoot {
    mainClassName = 'org.camunda.bpm.CamundaProcessApplication'
}

// boot run configuration for dynamic profiles
bootRun.ext.activeProfiles = 'common,db-local'
bootRun {
    doFirst {
        jvmArgs = ["-Dspring.profiles.active=" + ext.activeProfiles]
    }
}

// Run against a local in-memory database
task bootRunWithLocalDb {
    group = 'camunda'
    description "Alias to \":${bootRun.name}\" task: ${bootRun.description}"
    doFirst {
        bootRun.ext.activeProfiles = 'common,db-local'
    }
}
bootRunWithLocalDb.finalizedBy bootRun

// Run against remote database
task bootRunWithRemoteDb {
    group = 'camunda'
    description "Alias to \":${bootRun.name}\" task: ${bootRun.description}"
    doFirst {
        bootRun.ext.activeProfiles = 'common,db-remote'
    }
}
bootRunWithRemoteDb.finalizedBy bootRun
